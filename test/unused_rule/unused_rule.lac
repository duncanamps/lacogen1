//-----------------------------------------------------------------------------------
//
// LaCoGen - Grammar definition
//
// 04/04/2020
//
// -----------------------------------------------------------------------------------
  
%title          = "LaCoGen Grammar";
%version        = "0.5";
%author         = "Duncan Munro";
%codeprefix     = "LCG";
%start          = <Content>;


// ====================================================================
// Predefined Character sets
// ====================================================================

{Alphanumeric}  = [0-9A-Za-z];
{Letter}		= [A-Za-z];
{Number}		= [0-9];
{Printable}     = [ -~] + [\xa0];
{WSChar}        = [\t\r\n ];


// ====================================================================
// Character sets
// ====================================================================

{Backslash}         = [\x5c];
{EscapedChars}      = [tnrx\\\-\[\]]; 
{NonTerminal Ch}    = {Alphanumeric} + [_-.] + [ ];
{Set Literal Ch}    = {Printable} - [\]];
{Set Name Ch}       = {Printable} - [{}];
{String Ch}         = {Printable} - ["] - ['];
{Terminal St}       = {Letter};
{Terminal Ch}		= {Alphanumeric} + [_-.]; 
{Zero}				= [0]; 
{Nonzero Digit}		= {Number} - {Zero};

{Compilable}		= {Printable} + {WSChar};
{Non Asterisk}		= {Compilable} - [*];
{Non SlashAst}		= {Compilable} - [*/];
{Non Linebreak}		= {Compilable} - [\r\n];

 
// ====================================================================
// Terminals
// ====================================================================

TNonTerminal			= "<" {NonTerminal Ch}+ ">";
Terminal				= {Terminal St} {Terminal Ch}*; 
SetLiteral				= "[" ({Set Literal Ch} | ({Backslash} {EscapedChars}) )* "]";
SetName					= "{" {Set Name Ch}+ "}";
StringLiteral			= [\x22] ( {String Ch} | {Backslash} {Printable} )* [\x22];
Integer					= "-"? ({Zero} | {Nonzero Digit} {Number}*);

ParameterDefBoolean		= "%fastdfa"
						| "%skipwhitespace"
						;
 
ParameterDefInteger		= "%lexer_buffer"
						| "%lexer_tab"
						;
 
ParameterDefString		= "%author"
						| "%codeprefix"
						| "%copyright"
						| "%license"
						| "%title"
						| "%unit_lexer"
						| "%unit_parser"
						| "%version"
						;

ParameterDefNonTerminal	= "%start";

BlockComment			= "/*" ( {Non Asterisk}* [*]+ {Non SlashAst} )* {Non Asterisk}* [*]+ "/" ignore;
LineComment				= "//" {Non Linebreak}* ([\r] [\n] | [\r] | [\n]) ignore;
WhiteSpace				= {WSChar}+ ignore;


// ====================================================================
// Rules
// ====================================================================

// High level rules

<Content>			: <Content> <BoundedDefinition>				# ActIgnore
					| <BoundedDefinition>						# ActIgnore
					;

<BoundedDefinition>	: <Definition> ";"							# ActIgnore
					; 

<Definition>		: <ParameterDef>							# ActIgnore
					| <CharacterSetDef>							# ActIgnore
					| <TerminalDef>								# ActIgnore
					| <RuleDef>									# ActIgnore
					;


// Rules for Parameters

<ParameterDef>		: ParameterDefBoolean "=" <BooleanTerminal>	# ActParameterDefBoolean
					| ParameterDefInteger "=" Integer			# ActParameterDefInteger
					| ParameterDefString "=" StringLiteral		# ActParameterDefString
					| ParameterDefNonTerminal "=" TNonTerminal	# ActParameterDefNonTerminal
					;

<BooleanTerminal>	: "true"									# ActCopy
					| "false"									# ActCopy
					;
 

// Rules for Character Sets

<CharacterSetDef>	: SetName "=" <CSContent>					# ActSetDefine
					;
 
<CSContent>			: <CSContent> "+" <CSDef>					# ActSetAdd
					| <CSContent> "-" <CSDef>					# ActSetSub
					| <CSDef>									# ActSetUse
					;
 
<CSDef>				: SetName									# ActCopy
					| SetLiteral								# ActSetLiteral
					;


// Rules for Terminals

<TerminalDef>		: Terminal "=" <TerminalContent> "ignore"	# ActTerminalDefIgnore
					| Terminal "=" <TerminalContent>			# ActTerminalDef
					;
 
<TerminalContent>	: <TerminalContent> "|" <TerminalElement>	# ActTerminalOr
					| <TerminalElement>							# ActCopy
					;
 
<TerminalElement>	: <TerminalElement> <TerminalClosed>		# ActTerminalConcatenate
					| <TerminalClosed>							# ActCopy
					;
 
<TerminalClosed>	: <TerminalBracketed> "?"					# ActTerminalClosedBracketedOpt
					| <TerminalBracketed> "+"					# ActTerminalClosedBracketedPlus
					| <TerminalBracketed> "*"					# ActTerminalClosedBracketedStar
					| <TerminalBracketed>						# ActCopy
					;
 
<TerminalBracketed>	: "(" <TerminalContent> ")"					# ActTerminalBracketBracket
					| Terminal									# ActTerminalBracketTerminal
					| StringLiteral								# ActTerminalStringLiteral
					| SetName									# ActTerminalBracketSetName
					| SetLiteral								# ActTerminalBracketSetLiteral
					;


// Main rules

<RuleDef>			: <NonTerminal> ":" <RuleList> 				# ActRuleDef
					;

<RuleList>			: <RuleList> "|" <Rule>						# ActRuleListOrRule
					| <RuleList> "|" <OptProc>					# ActRuleListOrOptProc
					| <Rule>									# ActCopy
					;

<Rule> 				: <RuleBody> <OptProc> 						# ActRuleRuleBodyOptProc
					;
 
<RuleBody> 			: <RuleBody> <RuleAtom>						# ActRuleBodyRuleAtom
					| <RuleAtom>								# ActCopy
					;

<RuleAtom> 			: <NonTerminal>								# ActCopy
					| Terminal									# ActRuleAtomTerminal
					| StringLiteral								# ActRuleAtomStringLiteral
					;

<NonTerminal> 		: TNonTerminal 								# ActRuleAtomNonTerminal
					;
 
<OptProc> 			: "#" Terminal								# ActOptProcHashTerminal
					|											# ActIgnore
					;
			
<Bad>				: "!" Terminal
					;
					