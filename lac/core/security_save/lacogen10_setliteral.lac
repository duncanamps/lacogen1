//-----------------------------------------------------------------------------------
//
// LaCoGen - Grammar definition for set literals
//
//
// Examples are:
//
//   [0123456789]  // The digits
//   [0-9]         // Digits done a different way
//   [^0-9]        // Anything but digits
//   [A-Za-z]      // Letters
//   [\t]          // Tab
//   [\n]          // Newline
//   [\r]          // Carriage return
//   [\x00]        // Character 0 (hex)
//   [\x20]        // Space character
//   [\x30-\x39]   // Digits done another different way
//   [\x20ac]      // 16 bit character
//   [\\]          // Backslash character
//   [\]]          // Closing square bracket
//   [\-]          // Hyphen character
//   [\^]          // Caret character
//
// -----------------------------------------------------------------------------------
  
%title        = "LaCoGen Grammar - Set literal definition";
%version      = "0";
%author       = "Duncan Munro";
%codeprefix   = "LCG_LITERAL";
%start        = <Grammar>;


// ====================================================================
// Predefined Character sets
// ====================================================================

{Printable} = [ -\x07ff];
{Number}    = [0-9];

// ====================================================================
// Character sets
// ====================================================================

{EscapedControl} = [\\\-] + [\[\]];
{EscapedSingles} = [tnr];
{EscapedAll}     = {EscapedControl} + {EscapedSingles};
{NonEscaped}     = {Printable} - {EscapedControl};
{HexDigit}       = {Number} + [ABCDEFabcdef];
                                  
// ====================================================================
// Terminals
// ====================================================================

CharHex			= "\x" {HexDigit} {HexDigit} {HexDigit} {HexDigit} | "\x" {HexDigit} {HexDigit};      
CharNonEscaped	= {NonEscaped};
CharEscaped		= "\" {EscapedAll};


// ====================================================================
// Rules
// ====================================================================

<Grammar>		: "[" <LiteralDefs> "]"			# ActLitSetDefs
				| "[]"							# ActLitSetEmpty
				|								# ActLitIgnore
				;

<LiteralDefs>	: <LiteralDefs> <LiteralDef>	# ActLitIgnore
				| <LiteralDef>					# ActLitIgnore
				;

<LiteralDef>	: <Character> "-" <Character>	# ActLitLiteralRange
				| <Character>					# ActLitLiteralSingle
				;
                           
<Character>		: CharNonEscaped				# ActLitCharNonEscaped
				| CharHex						# ActLitCharHex
				| CharEscaped					# ActLitCharEscaped
				;
