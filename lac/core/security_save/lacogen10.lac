//-----------------------------------------------------------------------------------
//
//  LaCoGen V1.0 - Grammar definition
//
//  04/04/2020
//
//  Items over V0.5 grammar include:
//
//    Parameter definition keywords removed
//    Block comments and line comments commands
//
//  Current list of parameters allowed with [default] if set:
//
//    Boolean		%COMMENTMATCH [True] +
//					%COMMENTNESTED [True] +
//					%FASTLEXER [True] *
//					%FASTPARSER [True] *
//					
//	  String		%AUTHOR
//					%COMMENTLINE +
//					%CODEPREFIX
//					%COPYRIGHT
//					%LICENSE
//					%LICENSEURL +
//					%TITLE
//					%UNITLEXER *
//					%UNITPARSER *
//					%VERSION
//
//    String x 2	%COMMENTBLOCK +
//
//    Integer		%LEXERBUFFER [4096]
//					%LEXERTAB [4]
//					%PARSERBUFER [1024]
//
//    Non-Terminal  %START
//
//  + Items new to version 1.0
//  * Deprecated items, no longer used
//
// -----------------------------------------------------------------------------------
  
%title          = "LaCoGen Grammar";
%version        = "0.5";
%author         = "Duncan Munro";
%codeprefix     = "LCG";
%start          = <Content>;


// ====================================================================
// Predefined Character sets
// ====================================================================

{Alphanumeric}  = [0-9A-Za-z];
{Letter}		= [A-Za-z];
{Number}		= [0-9];
{Printable}     = [ -\x7f];
{WSChar}        = [\t\r\n ];


// ====================================================================
// Character sets
// ====================================================================

{Escape}			= [\x5c];  // Backslash
{Quote}				= ["];
{EscapedChars}      = [tnrx\\\-\[\]]; 
{NonTerminal Ch}    = {Alphanumeric} + [_\-.] + [ ];
{Set Literal Ch}    = {Printable} - [\]] - {Escape};
{Set Name Ch}       = {Printable} - [{}];
{String Ch}         = {Printable} - {Quote};
{Terminal St}       = {Letter};
{Terminal Ch}		= {Alphanumeric} + [_\-.]; 
{Zero}				= [0]; 
{Nonzero Digit}		= {Number} - {Zero};

{Compilable}		= {Printable} + {WSChar};
{Non Asterisk}		= {Compilable} - [*];
{Non SlashAst}		= {Compilable} - [*/];
{Non Linebreak}		= {Compilable} - [\r\n];

 
// ====================================================================
// Terminals
// ====================================================================

TNonTerminal			= "<" {NonTerminal Ch}+ ">";
Terminal				= {Terminal St} {Terminal Ch}*; 
SetLiteral				= "[" ({Set Literal Ch} | ({Escape} {EscapedChars}) )* "]";
SetName					= "{" {Set Name Ch}+ "}";
StringLiteral			= ["] ({String Ch} | {Escape} {EscapedChars})* ["];
Integer					= "-"? ({Zero} | {Nonzero Digit} {Number}*);

BlockComment			= "/*" ( {Non Asterisk}* [*]+ {Non SlashAst} )* {Non Asterisk}* [*]+ "/" ignore;
LineComment				= "//" {Non Linebreak}* ([\r] [\n] | [\r] | [\n]) ignore;
WhiteSpace				= {WSChar}+ ignore;

ParameterDefBoolean		= "%fastlexer"
						| "%fastparser"
						;
 
ParameterDefInteger		= "%lexerbuffer"
						| "%lexertab"
						;
 
ParameterDefString		= "%author"
						| "%codeprefix"
						| "%copyright"
						| "%license"
						| "%linecomment"
						| "%title"
						| "%unitlexer"
						| "%unitparser"
						| "%version"
						;
						
ParameterDefString2		= "%blockcomment"
						;

ParameterDefNonTerminal	= "%start";


// ====================================================================
// Rules
// ====================================================================

// High level rules

<Content>			: <Content> <BoundedDefinition>				# ActIgnore
					| <BoundedDefinition>						# ActIgnore
					;

<BoundedDefinition>	: <Definition> ";"							# ActIgnore
					; 

<Definition>		: <ParameterDef>							# ActIgnore
					| <CharacterSetDef>							# ActIgnore
					| <TerminalDef>								# ActIgnore
					| <RuleDef>									# ActIgnore
					;


// Rules for Parameters

<ParameterDef>		: ParameterDefBoolean		"=" <BooleanTerminal>	   		# ActParameterDefBoolean
					| ParameterDefInteger		"=" Integer			    		# ActParameterDefInteger
					| ParameterDefString		"=" StringLiteral		    	# ActParameterDefString
					| ParameterDefString2		"=" StringLiteral StringLiteral 	# ActParameterDefString2
					| ParameterDefNonTerminal	"=" TNonTerminal	        		# ActParameterDefNonTerminal
					;

<BooleanTerminal>	: "true"									# ActCopy
					| "false"									# ActCopy
					;
 

// Rules for Character Sets

<CharacterSetDef>	: SetName "=" <CSContent>					# ActSetDefine
					;
 
<CSContent>			: <CSContent> "+" <CSDef>					# ActSetAdd
					| <CSContent> "-" <CSDef>					# ActSetSub
					| <CSDef>									# ActSetUse
					;
 
<CSDef>				: SetName									# ActCopy
					| SetLiteral								# ActSetLiteral
					;


// Rules for Terminals

<TerminalDef>		: Terminal "=" <TerminalContent> "ignore"	# ActTerminalDefIgnore
					| Terminal "=" <TerminalContent> "keyword"	# ActTerminalDefKeyword
					| Terminal "=" <TerminalContent> "symbol"	# ActTerminalDefSymbol
					| Terminal "=" <TerminalContent>			# ActTerminalDef
					| Terminal "virtual"						# ActTerminalDefVirtual
					;
 
<TerminalContent>	: <TerminalContent> "|" <TerminalElement>	# ActTerminalOr
					| <TerminalElement>							# ActCopy
					;
 
<TerminalElement>	: <TerminalElement> <TerminalClosed>		# ActTerminalConcatenate
					| <TerminalClosed>							# ActCopy
					;
 
<TerminalClosed>	: <TerminalBracketed> "?"					# ActTerminalClosedBracketedOpt
					| <TerminalBracketed> "+"					# ActTerminalClosedBracketedPlus
					| <TerminalBracketed> "*"					# ActTerminalClosedBracketedStar
					| <TerminalBracketed>						# ActCopy
					;
 
<TerminalBracketed>	: "(" <TerminalContent> ")"					# ActTerminalBracketBracket
					| Terminal									# ActTerminalBracketTerminal
					| StringLiteral								# ActTerminalStringLiteral
					| SetName									# ActTerminalBracketSetName
					| SetLiteral								# ActTerminalBracketSetLiteral
					;


// Main rules

<RuleDef>			: <NonTerminal> ":" <RuleList> 				# ActRuleDef
					;

<RuleList>			: <RuleList> "|" <Rule>						# ActRuleListOrRule
					| <RuleList> "|" <OptProc>					# ActRuleListOrOptProc
					| <Rule>									# ActCopy
					;

<Rule> 				: <RuleBody> <OptProc> 						# ActRuleRuleBodyOptProc
					;
 
<RuleBody> 			: <RuleBody> <RuleAtom>						# ActRuleBodyRuleAtom
					| <RuleAtom>								# ActCopy
					;

<RuleAtom> 			: <NonTerminal>								# ActCopy
					| Terminal									# ActRuleAtomTerminal
					| StringLiteral								# ActRuleAtomStringLiteral
					;

<NonTerminal> 		: TNonTerminal 								# ActRuleAtomNonTerminal
					;
 
<OptProc> 			: "#" Terminal								# ActOptProcHashTerminal
					|											# ActIgnore
					;